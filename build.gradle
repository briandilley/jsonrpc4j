buildscript {

    ext {
        // Sync with https://github.com/spring-projects/spring-boot/blob/3.4.x/gradle.properties
        set("jacksonVersion", "2.18.4")
        set("springVersion", "6.2.7")
        set("springBootVersion", "3.4.5")
        set("jettyVersion", "12.0.21")
        set("slf4jVersion", "2.0.17")
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
        classpath("com.adarshr:gradle-test-logger-plugin:4.0.0")
    }
}

plugins {
    id('jacoco')
}

repositories {
    mavenLocal()
    gradlePluginPortal()
    mavenCentral()
}

apply plugin: "java"
apply plugin: "com.adarshr.test-logger"

group = "com.github.briandilley.jsonrpc4j"
version = "1.8.0-SNAPSHOT"
description = """
This project aims to provide the facility to easily implement JSON-RPC for the java programming language.
"""

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

ext {
    releaseVersion = !version.toString().endsWith('-SNAPSHOT')
}

test {
    testLogging {
        exceptionFormat 	= "FULL"
        showExceptions 		= true
        showStackTraces 	= true
        showCauses 			= true
    }
    maxParallelForks = 1
    forkEvery = 1
    maxHeapSize = "2g"
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

java {
    registerFeature('servletSupport') {
        // TODO: create a separate sourceSet for this library feature.
        //  Gradle is planning to break this in v9.0
        usingSourceSet(sourceSets.main)
    }
    registerFeature('springSupport') {
        // TODO: create a separate sourceSet for this library feature.
        //  Gradle is planning to break this in v9.0
        usingSourceSet(sourceSets.main)
    }
}

dependencies {

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    // TODO: remove deprecated portlet-api when support is removed from the code
    servletSupportImplementation 'javax.portlet:portlet-api:3.0.1'
    servletSupportImplementation 'javax.servlet:javax.servlet-api:4.0.1'
    // Sync with library("Jakarta Servlet", "6.0.0"): https://github.com/spring-projects/spring-boot/blob/3.4.x/spring-boot-project/spring-boot-dependencies/build.gradle#L770
    servletSupportImplementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    implementation group: 'jakarta.jws', name: 'jakarta.jws-api', version: '3.0.0'

    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    springSupportImplementation "org.springframework:spring-core:${springVersion}"
    springSupportImplementation "org.springframework:spring-context:${springVersion}"
    springSupportImplementation "org.springframework:spring-web:${springVersion}"
    springSupportImplementation "org.springframework:spring-webmvc:${springVersion}"

    // Sync with https://github.com/spring-projects/spring-boot/blob/3.4.x/gradle.properties
    implementation 'commons-codec:commons-codec:1.17.2'
    // TODO: HttpCore 5 has been released in 2020 https://hc.apache.org/httpcomponents-core-5.3.x/
    implementation 'org.apache.httpcomponents:httpcore-nio:4.4.16'

    // TODO: JUnit 5 has been released in 2017 https://www.baeldung.com/junit-5
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.easymock:easymock:5.5.0'
    testImplementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}") {
        exclude module: 'logback-classic'
    }
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation("org.eclipse.jetty:jetty-server:${jettyVersion}") {
        exclude module: 'javax.servlet'
    }
    testImplementation("org.eclipse.jetty:jetty-servlet:${jettyVersion}") {
        exclude module: 'org.eclipse.jetty.orbit'
    }
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.24.3'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.24.3'

}


jar {
    manifest {
       attributes 'Automatic-Module-Name': 'jsonrpc4j'
   }
}

task documentationJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives documentationJar, sourcesJar
}

apply from: 'publishing.gradle'
