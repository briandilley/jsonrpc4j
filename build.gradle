buildscript {

    ext {
        set("jacksonVersion", "2.16.1")
        // Versions 6.0.0 and above no longer support Java 1.8
        set("springVersion", "5.3.32")
        // Versions 3.0.0 and above no longer support Java 1.8
        set("springBootVersion", "2.7.18")
        set("jettyVersion", "9.4.54.v20240208")
        set("slf4jVersion", "2.0.12")
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
        classpath("com.adarshr:gradle-test-logger-plugin:3.2.0")
    }
}

plugins {
    id('jacoco')
}

repositories {
    mavenLocal()
    gradlePluginPortal()
    mavenCentral()
}

apply plugin: "java"
apply plugin: "com.adarshr.test-logger"

group = "com.github.briandilley.jsonrpc4j"
version = "1.8.0-SNAPSHOT"
description = """
This project aims to provide the facility to easily implement JSON-RPC for the java programming language.
"""

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

ext {
    releaseVersion = !version.toString().endsWith('-SNAPSHOT')
}

test {
    testLogging {
        exceptionFormat 	= "FULL"
        showExceptions 		= true
        showStackTraces 	= true
        showCauses 			= true
    }
    maxParallelForks = 1
    forkEvery = 1
    maxHeapSize = "2g"
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

java {
    registerFeature('servletSupport') {
        // TODO: create a separate sourceSet for this library feature.
        //  Gradle is planning to break this in v9.0
        usingSourceSet(sourceSets.main)
    }
    registerFeature('springSupport') {
        // TODO: create a separate sourceSet for this library feature.
        //  Gradle is planning to break this in v9.0
        usingSourceSet(sourceSets.main)
    }
}

dependencies {

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    // TODO: remove deprecated portlet-api when support is removed from the code
    servletSupportImplementation 'javax.portlet:portlet-api:3.0.1'
    servletSupportImplementation 'javax.servlet:javax.servlet-api:4.0.1'
    // TODO: Jakarta EE 9 and jakarta.servlet-api 5.x are still compatible with Java SE 8,
    // update jakarta.servlet-api to version 6+ when JDK baseline is increased to 11+
    servletSupportImplementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'

    implementation group: 'jakarta.jws', name: 'jakarta.jws-api', version: '3.0.0'

    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    springSupportImplementation "org.springframework:spring-core:${springVersion}"
    springSupportImplementation "org.springframework:spring-context:${springVersion}"
    springSupportImplementation "org.springframework:spring-web:${springVersion}"
    springSupportImplementation "org.springframework:spring-webmvc:${springVersion}"

    implementation 'commons-codec:commons-codec:1.16.1'
    implementation 'org.apache.httpcomponents:httpcore-nio:4.4.16'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.easymock:easymock:5.2.0'
    testImplementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}") {
        exclude module: 'logback-classic'
    }
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation("org.eclipse.jetty:jetty-server:${jettyVersion}") {
        exclude module: 'javax.servlet'
    }
    testImplementation("org.eclipse.jetty:jetty-servlet:${jettyVersion}") {
        exclude module: 'org.eclipse.jetty.orbit'
    }
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.23.0'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.23.0'

}


jar {
    manifest {
       attributes 'Automatic-Module-Name': 'jsonrpc4j'
   }
}

task documentationJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives documentationJar, sourcesJar
}

apply from: 'publishing.gradle'
